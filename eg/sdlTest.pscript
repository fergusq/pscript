module sdlTest;

import core;

extern import "SDL/SDL.h";

extern struct SDL_Surface {
	Int w;
	Int h;
}

extend SDL_Surface : String {
	Str toString() {
		return "SDL_Surface {}";
	}
}

extern struct SDL_Rect {
	Int x;
	Int y;
	Int w;
	Int h;
}

extern Int SDL_Init(Int flags);
extern SDL_Surface SDL_SetVideoMode(Int x, Int y, Int c, Int flags);
extern SDL_Surface SDL_LoadBMP(Str filename);
extern Void SDL_BlitSurface(SDL_Surface image, Int i, SDL_Surface surface, SDL_Rect area);
extern Void SDL_Flip(SDL_Surface surface);
extern Void SDL_FreeSurface(SDL_Surface surface);
extern Void SDL_Delay(Int time);
extern Int SDL_GetTicks();
extern Void SDL_Quit();

model Application {
	Void quit();
	
	Sprite loadSprite(String filename, Int width, Int height);
	Void drawSprites();
}

struct SDLApplication {
	SDL_Surface surface;
	ArrayList<SDL_Surface> textures;
	ArrayList<Sprite> sprites;
}

extend SDLApplication : Application {
	Void quit() {
		for (texture in this.textures.elements) SDL_FreeSurface(texture);
		SDL_Quit();
	}
	
	Sprite loadSprite(String filename, Int width, Int height) {
		// TODO: cache textures
		var texture = SDL_LoadBMP(filename.toString());
		this.textures.push(texture);
		
		var sprite = new Sprite { 0, 0, width, height, texture };
		this.sprites.push(sprite);
		
		return sprite;
	}
	
	Void drawSprites() {
		for (sprite in this.sprites.elements) {
			var area = new SDL_Rect { sprite.x, sprite.y, sprite.width, sprite.height };
			SDL_BlitSurface(sprite.texture, 0, this.surface, area);
		}
		SDL_Flip(this.surface);
	}
}

SDLApplication createSDLApplication(Int width, Int height) {
	SDL_Init(32);
	return new SDLApplication {
		SDL_SetVideoMode(width, height, 32, 1073741825),
		createArrayList(),
		createArrayList()
	};
}

struct Sprite {
	Int x;
	Int y;
	Int width;
	Int height;
	SDL_Surface texture;
}

extend Sprite : String {
	Str toString() {
		return "Sprite {}";
	}
}

extend Sprite : Eq<Sprite> {
	Bool operator ==(Sprite s) {
		return this.x == s.x && this.y == s.y
			&& this.width == s.width
			&& this.height == s.height;
	}
	Bool operator !=(Sprite s) {
		return !(this == s);
	}
}

model Updater {
	Bool isRunning();
	Void updateWorld(Int n, Float time);
	Void updateSprites(Int n, Float time);
}

Void main(Int argc, Str* argv) {
	var args = new Array<Str> { argc, argv };
	var app = createSDLApplication(1024, 768);
	
	var updateInterval = 1000; // 1 s
	var updates = 0;
	var startTime = SDL_GetTicks();
	
	var updater = new MyUpdater { app.loadSprite("logo.bmp", 100, 100), true };
	while (updater.isRunning()) {
		var time = SDL_GetTicks();
		var timeSinceStart = (time-startTime);
		var modTime = (timeSinceStart % updateInterval) as Float / updateInterval;
		
		while (timeSinceStart / updateInterval > updates) {
			updates = updates + 1;
			updater.updateWorld(updates, modTime);
		}
		
		updater.updateSprites(updates, modTime);
		app.drawSprites();
		SDL_Delay(1);
	}
	app.quit();
}

struct MyUpdater {
	Sprite logo;
	Bool running;
}

extend MyUpdater : Updater {
	Bool isRunning() {
		return this.running;
	}
	Void updateWorld(Int update, Float time) {
		this.running = update <= 10;
	}
	Void updateSprites(Int update, Float time) {
		var t = (time+update)*10;
		this.logo.x = t*10;
		t = t*t;
		var i = t as Int % 434;
		if (t as Int / 434 % 2 == 0) {
			this.logo.y = i;
		} else {
			this.logo.y = 434-i;
		}
	}
}

//extern import "stdio.h";
//extern Void puts(Str str);
